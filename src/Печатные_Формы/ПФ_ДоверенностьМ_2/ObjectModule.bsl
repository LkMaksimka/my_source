
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция СведенияОВнешнейОбработке() Экспорт

    МассивНазначений = Новый Массив;
    МассивНазначений.Добавить("Документ.ДоверенностьВыданная");

    ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке("2.2.2.1");
    Информация = НСтр("ru = 'Доверенность - изменения в шапке и в подвале :) '");
    ПараметрыРегистрации.Информация = Информация;
    ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиПечатнаяФорма();
    ПараметрыРегистрации.Версия = "1.0.0.1";
    ПараметрыРегистрации.БезопасныйРежим = Истина;
    ПараметрыРегистрации.Вставить("Назначение", МассивНазначений);

    Команда = ПараметрыРегистрации.Команды.Добавить();
    Команда.Представление = НСтр("ru = '(ТН) Доверенность (М2)'");
    Команда.Идентификатор = "пф_ДоверенностьМ2";
    Команда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовСерверногоМетода();
    Команда.ПоказыватьОповещение = Ложь;
    
    Команда = ПараметрыРегистрации.Команды.Добавить();
    Команда.Представление = НСтр("ru = '(ТН) Доверенность (М2-а)'");
    Команда.Идентификатор = "пф_ДоверенностьМ2а";
    Команда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовСерверногоМетода();
    Команда.ПоказыватьОповещение = Ложь;

    Возврат ПараметрыРегистрации;

КонецФункции


Процедура Печать(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода = Неопределено)  экспорт 

    ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;

    Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "пф_ДоверенностьМ2") Тогда	

  		    УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
                            "пф_ДоверенностьМ2", 
                            "пф_ДоверенностьМ2", 
                            СформироватьПечатнуюФорму(МассивОбъектов, ОбъектыПечати, "М2")); 
    КонецЕсли;

     Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "пф_ДоверенностьМ2а") Тогда	

  		    УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
                            "пф_ДоверенностьМ2а", 
                            "пф_ДоверенностьМ2а", 
                            СформироватьПечатнуюФорму(МассивОбъектов, ОбъектыПечати, "М2а")); 
    КонецЕсли;

КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Функция СформироватьПечатнуюФорму(МассивОбъектов, ОбъектыПечати, НазваниеФормы)	

	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("Имя", "СтруктурноеПодразделение_3302a3e05c8545bbac62ae918b182774");
	Запрос.Текст = 
	
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА СтруктураПредприятияДополнительныеРеквизиты.Значение ССЫЛКА Справочник.Организации
	|			ТОГДА ВЫРАЗИТЬ(СтруктураПредприятияДополнительныеРеквизиты.Значение КАК Справочник.Организации).НаименованиеПолное
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК Значение,
	|	СтруктураПредприятияДополнительныеРеквизиты.Ссылка КАК ПодразделениеСсылка
	|ПОМЕСТИТЬ ВТ_ДополнительныхСвойств
	|ИЗ
	|	Справочник.СтруктураПредприятия.ДополнительныеРеквизиты КАК СтруктураПредприятияДополнительныеРеквизиты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|		ПО СтруктураПредприятияДополнительныеРеквизиты.Свойство = ДополнительныеРеквизитыИСведения.Ссылка
	|			И (ДополнительныеРеквизитыИСведения.Имя = &Имя)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Ссылка КАК Ссылка,
	|	ВложенныйЗапрос.Номер КАК Номер,
	|	ВложенныйЗапрос.ДатаДокумента КАК ДатаДокумента,
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.Префикс КАК Префикс,
	|	ВложенныйЗапрос.Руководитель КАК Руководитель,
	|	ВложенныйЗапрос.ДолжностьРуководителя КАК ДолжностьРуководителя,
	|	ВложенныйЗапрос.ГлавныйБухгалтер КАК ГлавныйБухгалтер,
	|	ВложенныйЗапрос.ФизЛицо КАК ФизЛицо,
	|	ВложенныйЗапрос.РеквизитыДокументаНаПолучение КАК РеквизитыДокументаНаПолучение,
	|	ВложенныйЗапрос.СрокДействия КАК СрокДействия,
	|	ВложенныйЗапрос.Должность КАК Должность,
	|	ВложенныйЗапрос.Ответственный КАК Ответственный,
	|	ВложенныйЗапрос.БанковскийСчет КАК БанковскийСчет,
	|	ВложенныйЗапрос.ПоставщикПредставление КАК ПоставщикПредставление,
	|	ВложенныйЗапрос.ДокументОснование КАК ДокументОснование,
	|	ВложенныйЗапрос.НомерОснования КАК НомерОснования,
	|	ВложенныйЗапрос.ДатаОснования КАК ДатаОснования,
	|	ВложенныйЗапрос.ПрефиксОснования КАК ПрефиксОснования,
	|	ВложенныйЗапрос.Товары.(
	|		Номер КАК Номер,
	|		НоменклатураПоставщика КАК НоменклатураПоставщика,
	|		Номенклатура КАК Номенклатура,
	|		Характеристика КАК Характеристика,
	|		Упаковка КАК Упаковка,
	|		ЕдиницаИзмеренияПредставление КАК ЕдиницаИзмеренияПредставление,
	|		КоличествоУпаковок КАК КоличествоУпаковок
	|	) КАК Товары,
	|	ВложенныйЗапрос.МатериальныеЦенности.(
	|		Номер КАК Номер,
	|		МатериальнаяЦенность КАК МатериальнаяЦенность,
	|		Количество КАК Количество,
	|		ЕдиницаИзмеренияПредставление КАК ЕдиницаИзмеренияПредставление
	|	) КАК МатериальныеЦенности,
	|	ВложенныйЗапрос.ВидДокумента КАК ВидДокумента,
	|	ВложенныйЗапрос.ПаспортСерия КАК ПаспортСерия,
	|	ВложенныйЗапрос.ПаспортНомер КАК ПаспортНомер,
	|	ВложенныйЗапрос.ПаспортВыдан КАК ПаспортВыдан,
	|	ВложенныйЗапрос.ПаспортДатаВыдачи КАК ПаспортДатаВыдачи,
	|	ВложенныйЗапрос.СуммаДокумента КАК СуммаДокумента,
	|	ВложенныйЗапрос.ВалютаПредставление КАК ВалютаПредставление,
	|	ВложенныйЗапрос.СкладОтправитель КАК СкладОтправитель,
	|	ВложенныйЗапрос.Подразделение КАК Подразделение,
	|	ВложенныйЗапрос.ДокументЗаказНаПеремещение КАК ДокументЗаказНаПеремещение,
	|	ВТ_ДополнительныхСвойств.Значение КАК СтуктурноеПодразделение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Доверенность.Ссылка КАК Ссылка,
	|		Доверенность.Номер КАК Номер,
	|		Доверенность.Дата КАК ДатаДокумента,
	|		Доверенность.Организация КАК Организация,
	|		Доверенность.Организация.Префикс КАК Префикс,
	|		ТаблицаОтветственныеЛица.РуководительНаименование КАК Руководитель,
	|		ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителя,
	|		ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер,
	|		Доверенность.ФизЛицо КАК ФизЛицо,
	|		Доверенность.ПоДокументу КАК РеквизитыДокументаНаПолучение,
	|		Доверенность.ДатаОкончанияДействия КАК СрокДействия,
	|		Доверенность.Должность КАК Должность,
	|		Доверенность.Ответственный КАК Ответственный,
	|		Доверенность.БанковскийСчет КАК БанковскийСчет,
	|		Доверенность.Контрагент.НаименованиеПолное КАК ПоставщикПредставление,
	|		Доверенность.ДокументОснование КАК ДокументОснование,
	|		Доверенность.ДокументОснование.Номер КАК НомерОснования,
	|		Доверенность.ДокументОснование.Дата КАК ДатаОснования,
	|		Доверенность.ДокументОснование.Организация.Префикс КАК ПрефиксОснования,
	|		Доверенность.Товары.(
	|			НомерСтроки КАК Номер,
	|			ПРЕДСТАВЛЕНИЕ(Доверенность.Товары.НоменклатураПоставщика) КАК НоменклатураПоставщика,
	|			Номенклатура.НаименованиеПолное КАК Номенклатура,
	|			Характеристика.НаименованиеПолное КАК Характеристика,
	|			ВЫБОР
	|				КОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) = 1
	|					ТОГДА НЕОПРЕДЕЛЕНО
	|				ИНАЧЕ Доверенность.Товары.Упаковка.Наименование
	|			КОНЕЦ КАК Упаковка,
	|			&ТекстЗапросаНаименованиеЕдиницыИзмерения КАК ЕдиницаИзмеренияПредставление,
	|			КоличествоУпаковок КАК КоличествоУпаковок
	|		) КАК Товары,
	|		Доверенность.МатериальныеЦенности.(
	|			НомерСтроки КАК Номер,
	|			МатериальнаяЦенность КАК МатериальнаяЦенность,
	|			Количество КАК Количество,
	|			ПРЕДСТАВЛЕНИЕ(Доверенность.МатериальныеЦенности.ЕдиницаИзмерения) КАК ЕдиницаИзмеренияПредставление
	|		) КАК МатериальныеЦенности,
	|		Доверенность.ВидДокументаФизЛица КАК ВидДокумента,
	|		Доверенность.СерияДокументаФизЛица КАК ПаспортСерия,
	|		Доверенность.НомерДокументаФизЛица КАК ПаспортНомер,
	|		Доверенность.КемВыданДокументФизлица КАК ПаспортВыдан,
	|		Доверенность.ДатаВыдачиДокументаФизЛица КАК ПаспортДатаВыдачи,
	|		Доверенность.СуммаДокумента КАК СуммаДокумента,
	|		ПРЕДСТАВЛЕНИЕ(Доверенность.Валюта) КАК ВалютаПредставление,
	|		ВЫБОР
	|			КОГДА Доверенность.ДокументОснование.Ссылка ССЫЛКА Документ.ЗаказНаПеремещение
	|				ТОГДА ВЫРАЗИТЬ(Доверенность.ДокументОснование КАК Документ.ЗаказНаПеремещение).СкладОтправитель
	|			ИНАЧЕ """"
	|		КОНЕЦ КАК СкладОтправитель,
	|		ВЫБОР
	|			КОГДА Доверенность.ДокументОснование.Ссылка ССЫЛКА Документ.ЗаказНаПеремещение
	|				ТОГДА ВЫРАЗИТЬ(Доверенность.ДокументОснование КАК Документ.ЗаказНаПеремещение).Подразделение
	|			ИНАЧЕ """"
	|		КОНЕЦ КАК Подразделение,
	|		ВЫБОР
	|			КОГДА Доверенность.ДокументОснование.Ссылка ССЫЛКА Документ.ЗаказНаПеремещение
	|				ТОГДА ВЫРАЗИТЬ(Доверенность.ДокументОснование КАК Документ.ЗаказНаПеремещение).Ссылка
	|			ИНАЧЕ """"
	|		КОНЕЦ КАК ДокументЗаказНаПеремещение
	|	ИЗ
	|		Документ.ДоверенностьВыданная КАК Доверенность
	|			ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|			ПО Доверенность.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|	ГДЕ
	|		Доверенность.Ссылка В(&МассивОбъектов)) КАК ВложенныйЗапрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДополнительныхСвойств КАК ВТ_ДополнительныхСвойств
	|		ПО ВложенныйЗапрос.Подразделение = ВТ_ДополнительныхСвойств.ПодразделениеСсылка";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"Доверенность.Товары.Упаковка",
			"Номенклатура"));
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"Доверенность.Товары.Упаковка",
			"Доверенность.Товары.Номенклатура"));
	
	ДанныеПечати = Запрос.Выполнить().Выбрать();
	
	ПервыйДокумент = Истина;
	
	Макет = ПолучитьМакет("ПФ_MXL_М2_ru");
	Макет.КодЯзыка = Метаданные.Языки.Русский.КодЯзыка;
	
	Если НазваниеФормы = "М2" Тогда
		ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Доверенность_М2";
	ИначеЕсли НазваниеФормы = "М2а" Тогда
		ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Доверенность_М2а";
	КонецЕсли;
	
	Пока ДанныеПечати.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		НомерДокументаНаПечать        = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеПечати.Номер, Ложь, Истина);
		ФамилияИмяОтчествоДоверенного = ФизическиеЛицаУТ.ФамилияИнициалыФизЛица(ДанныеПечати.ФизЛицо, ДанныеПечати.ДатаДокумента);
		
		СведенияОбОрганизации = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(
			ДанныеПечати.Организация,
			ДанныеПечати.ДатаДокумента,
			,
			ДанныеПечати.БанковскийСчет);
		
		ПредставлениеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(
			СведенияОбОрганизации, 
			"ПолноеНаименование,ИНН,КПП,ЮридическийАдрес,Телефоны");
			
		КодПоОКУД = "";
		Если НазваниеФормы = "М2" Тогда
			
			ОбластьМакета = Макет.ПолучитьОбласть("Отрез");
			ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
			
			СтруктураДанныхОтрез = Новый Структура;
			СтруктураДанныхОтрез.Вставить("НомерДокумента", НомерДокументаНаПечать);
			СтруктураДанныхОтрез.Вставить("ФИОДоверенного", ?(ПустаяСтрока(ДанныеПечати.Должность), "", ДанныеПечати.Должность + " ") + ФамилияИмяОтчествоДоверенного);
			ОбластьМакета.Параметры.Заполнить(СтруктураДанныхОтрез);
			ТабличныйДокумент.Вывести(ОбластьМакета);

			КодПоОКУД = "0315001";
			
		ИначеЕсли НазваниеФормы = "М2а" Тогда
			
			КодПоОКУД = "0315002";
			
		КонецЕсли;
		
		// Заполнить реквизиты шапки
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		
		СтруктураДанныхШапка = Новый Структура;
		ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
		СтруктураДанныхШапка.Вставить("НомерДокумента", НомерДокументаНаПечать);
		СтруктураДанныхШапка.Вставить("ДатаДокумента", Формат(ДанныеПечати.ДатаДокумента,"ДФ = ""дд ММММ гггг """"г."""));
		СтруктураДанныхШапка.Вставить("СрокДействия", Формат(ДанныеПечати.СрокДействия,"ДФ = ""дд ММММ гггг """"г."""));
		СтруктураДанныхШапка.Вставить("ФамилияИмяОтчествоДоверенного", ДанныеПечати.ФизЛицо);
		СтруктураДанныхШапка.Вставить("ДолжностьДоверенного", ДанныеПечати.Должность);
		СтруктураДанныхШапка.Вставить("ОрганизацияПредставление", ПредставлениеОрганизации);
		СтруктураДанныхШапка.Вставить("РеквизитыПотребителя", ПредставлениеОрганизации);
		СтруктураДанныхШапка.Вставить("РеквизитыПлательщика", ПредставлениеОрганизации);
		СтруктураДанныхШапка.Вставить("ОрганизацияКодПоОКПО", СведенияОбОрганизации.КодПоОКПО);
		СтруктураДанныхШапка.Вставить("КодПоОКУД", КодПоОКУД);
		
		СтруктураДанныхШапка.Вставить("РеквизитыСчета", ФормированиеПечатныхФорм.ОписаниеОрганизации(
			СведенияОбОрганизации,
			"НомерСчета,Банк,БИК,КоррСчет,"));
		ОбластьМакета.Параметры.Заполнить(СтруктураДанныхШапка);
		ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабличныйДокумент, Макет, ОбластьМакета, ДанныеПечати.Ссылка);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		// Заполнить таблицу
		ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("СтрокаТаблицы");
		
		ВыборкаСтрокТовары = ДанныеПечати.Товары.Выбрать();
		ВыборкаСтрокМатериальныеЦенности = ДанныеПечати.МатериальныеЦенности.Выбрать();
		КоличествоСтрок = 0;
		НомерПоследнейСтроки = 0;
		Пока ВыборкаСтрокТовары.Следующий() Цикл
			
			ОбластьМакета.Параметры.Заполнить(ВыборкаСтрокТовары);
			
			Если ЗначениеЗаполнено(ВыборкаСтрокТовары.НоменклатураПоставщика) Тогда
				
				МатериальнаяЦенность = ВыборкаСтрокТовары.НоменклатураПоставщика;
				
			Иначе
				
				МатериальнаяЦенность = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
					ВыборкаСтрокТовары.Номенклатура,
					ВыборкаСтрокТовары.Характеристика);
				
			КонецЕсли;
			
			СтруктураДанныхСтрокаТаблицы = Новый Структура;
			СтруктураДанныхСтрокаТаблицы.Вставить("МатериальнаяЦенность", МатериальнаяЦенность);
			СтруктураДанныхСтрокаТаблицы.Вставить("КоличествоПрописью",
				?(ВыборкаСтрокТовары.КоличествоУпаковок = 0,
				"",
				Строка(ВыборкаСтрокТовары.КоличествоУпаковок) + " (" + 
				ФормированиеПечатныхФорм.КоличествоПрописью(ВыборкаСтрокТовары.КоличествоУпаковок) + ")"));
			
			ОбластьМакета.Параметры.Заполнить(СтруктураДанныхСтрокаТаблицы);
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			КоличествоСтрок = КоличествоСтрок + 1;
			
		КонецЦикла;
		
		Пока ВыборкаСтрокМатериальныеЦенности.Следующий() Цикл
			
			ОбластьМакета.Параметры.Заполнить(ВыборкаСтрокМатериальныеЦенности);
			СтруктураДанныхСтрокаТаблицы = Новый Структура;
			НомерПоследнейСтроки = ВыборкаСтрокМатериальныеЦенности.Номер + КоличествоСтрок;
			СтруктураДанныхСтрокаТаблицы.Вставить("Номер", НомерПоследнейСтроки);
			СтруктураДанныхСтрокаТаблицы.Вставить("КоличествоПрописью",
				?(ВыборкаСтрокМатериальныеЦенности.Количество = 0,
				"",
				Строка(ВыборкаСтрокМатериальныеЦенности.Количество) + " (" + 
				ФормированиеПечатныхФорм.КоличествоПрописью(ВыборкаСтрокМатериальныеЦенности.Количество) + ")"));
			ОбластьМакета.Параметры.Заполнить(СтруктураДанныхСтрокаТаблицы);
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЦикла;
		
		Если НЕ ДанныеПечати.СуммаДокумента = 0 Тогда
			
			СтруктураДанныхСтрокаТаблицы = Новый Структура;
			НомерПоследнейСтроки = НомерПоследнейСтроки + 1;
			СтруктураДанныхСтрокаТаблицы.Вставить("Номер", НомерПоследнейСтроки);
			СтруктураДанныхСтрокаТаблицы.Вставить("МатериальнаяЦенность", НСтр("ru = 'Наличные денежные средства';
																				|en = 'Cash'"));
			СтруктураДанныхСтрокаТаблицы.Вставить("КоличествоПрописью",
				?(ДанныеПечати.СуммаДокумента = 0,
				"",
				Строка(ДанныеПечати.СуммаДокумента) + " (" + 
				ФормированиеПечатныхФорм.КоличествоПрописью(ДанныеПечати.СуммаДокумента) + ")"));
			СтруктураДанныхСтрокаТаблицы.Вставить("ЕдиницаИзмеренияПредставление", ДанныеПечати.ВалютаПредставление);
			ОбластьМакета.Параметры.Заполнить(СтруктураДанныхСтрокаТаблицы);
			ТабличныйДокумент.Вывести(ОбластьМакета);
		КонецЕсли;
		
		// Заполнить реквизиты подвала
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
		
		СтруктураДанныхПодвал = Новый Структура;
		СтруктураДанныхПодвал.Вставить("ФИОРуководителя", ДанныеПечати.Руководитель);
		СтруктураДанныхПодвал.Вставить("ФИОГлавногоБухгалтера", ДанныеПечати.ГлавныйБухгалтер);
		ОбластьМакета.Параметры.Заполнить(СтруктураДанныхПодвал);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ТабличныйДокумент;

КонецФункции

#КонецОбласти


#Иначе

ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");

#КонецЕсли