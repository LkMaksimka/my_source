
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьПривязку(Команда)
	ВыполнитьПривязкуСервер();
КонецПроцедуры

&НаКлиенте
Процедура Отобрать(Команда)
    НеобходимыеПоляЗаполенены = ПроверитьЗаполнениеНеобходимыхПолей();
	Если НеобходимыеПоляЗаполенены Тогда
        Товары.Очистить();
		ОтобратьСервер();	
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура Привязать(Команда)
	ПривязатьСервер();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОтобратьСервер()
		
	Если ПолеНоменклатуры = "Модель" Тогда
		ИмяТаблицы = "НоменклатураДополнительныеРеквизиты";
		ТекстЗапроса =
        "ВЫБРАТЬ ПЕРВЫЕ 1
        |	ДополнительныеРеквизитыИСведения.Ссылка КАК Ссылка
        |ПОМЕСТИТЬ ВТ_СвойствоПоИмени
        |ИЗ
        |	ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
        |ГДЕ
        |	ДополнительныеРеквизитыИСведения.Имя = &ПолеНоменклатуры
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	НоменклатураДополнительныеРеквизиты.Ссылка КАК Номенклатура,
        |	ВЫРАЗИТЬ(НоменклатураДополнительныеРеквизиты.Значение КАК СТРОКА(150)) КАК ПредставлениеПоляНоменлатуры
        |ПОМЕСТИТЬ СпрНоменклатура
        |ИЗ
        |	Справочник.Номенклатура.ДополнительныеРеквизиты КАК НоменклатураДополнительныеРеквизиты
        |ГДЕ
        |	НоменклатураДополнительныеРеквизиты.Свойство В
        |			(ВЫБРАТЬ
        |				ВТ.Ссылка
        |			ИЗ
        |				ВТ_СвойствоПоИмени КАК ВТ)
        |	И &УсловиеГруппыТоваров";
	Иначе
		ИмяТаблицы = "СпрНоменклатура";
		ТекстЗапроса  =
		"ВЫБРАТЬ
		|	СпрНоменклатура.Ссылка КАК Номенклатура,
		|	&СпрНоменклатураПолеНоменклатуры КАК ПредставлениеПоляНоменлатуры
		|ПОМЕСТИТЬ СпрНоменклатура
		|ИЗ
		|	Справочник.Номенклатура КАК СпрНоменклатура
		|ГДЕ
		|	НЕ СпрНоменклатура.ПометкаУдаления
		|	И &УсловиеГруппыТоваров
		|	И &СпрНоменклатураПолеНоменклатуры <> """"
		|	И ПОДСТРОКА(&СпрНоменклатураПолеНоменклатуры, 4, 150) <> """"
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура";
	КонецЕсли; 	
    
    ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПредставлениеПоляНоменлатуры", ПолеНоменклатуры);
    ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СпрНоменклатураПолеНоменклатуры", "СпрНоменклатура." + ПолеНоменклатуры);
	Если ГруппаТоваров.Пустая() Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеГруппыТоваров", "");	
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеГруппыТоваров",
								  				 "И " + ИмяТаблицы + ".Ссылка В ИЕРАРХИИ(&ГруппаТоваров)");
	КонецЕсли; 
	
    МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
    Запрос = Новый Запрос;
 	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
    
    Запрос.Текст = ТекстЗапроса;

    Запрос.УстановитьПараметр("ГруппаТоваров", ГруппаТоваров);
    Запрос.УстановитьПараметр("ПолеНоменклатуры", ПолеНоменклатуры);
    
    Запрос.Выполнить();
		
		
	ТекстЗапроса =
	"ВЫБРАТЬ
    |   НоменклатураПоставщиков.Ссылка КАК НоменклатураПоставщика,
    |   &ПолеНоменклатурыПоставщика КАК ПредставлениеПолеНоменклатурыПоставщика
    |ПОМЕСТИТЬ СпрНоменклатураПоставщиков
    |ИЗ
    |   Справочник.НоменклатураПоставщиков КАК НоменклатураПоставщиков
    |ГДЕ
    |   НоменклатураПоставщиков.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
    |   И НЕ НоменклатураПоставщиков.ПометкаУдаления
    |   И &ПараметрПоПартнеру
    |   И &ПолеНоменклатурыПоставщика <> """"
    |   И ПОДСТРОКА(&ПолеНоменклатурыПоставщика, 4, 150) <> """"
    |
    |ИНДЕКСИРОВАТЬ ПО
    |   НоменклатураПоставщика
    |;
    |
    |////////////////////////////////////////////////////////////////////////////////
    |ВЫБРАТЬ ПЕРВЫЕ 1000
    |   СпрНоменклатура.Номенклатура КАК Номенклатура,
    |   НоменклатураПоставщиков.НоменклатураПоставщика КАК НоменклатураПоставщика
    |ИЗ
    |   СпрНоменклатураПоставщиков КАК НоменклатураПоставщиков
    |       ВНУТРЕННЕЕ СОЕДИНЕНИЕ СпрНоменклатура КАК СпрНоменклатура
    |       ПО (&УсловиеСоединения)
    |
    |УПОРЯДОЧИТЬ ПО
    |   &ПолеНоменклатуры";
    
    Если РежимПоиска = "Равно" Тогда
    	УсловиеСоединения = "&ПолеНоменклатурыПоставщика = &ПолеНоменклатуры";
       Иначе
        УсловиеСоединения = "&ПолеНоменклатурыПоставщика ПОДОБНО ""%"" + &ПолеНоменклатуры + ""%"" ";  
    КонецЕсли; 
    ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеСоединения", УсловиеСоединения);
    
    ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеНоменклатурыПоставщика", 
                                             "НоменклатураПоставщиков." + ПолеНоменклатурыПоставщика);
    ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПредставлениеПолеНоменклатурыПоставщика", ПолеНоменклатурыПоставщика);
    ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеНоменклатуры", "СпрНоменклатура." + ПолеНоменклатуры);

    Если Партнер.Пустая() Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &ПараметрПоПартнеру", "");
	Иначе 
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПараметрПоПартнеру"," НоменклатураПоставщиков.Владелец = &Партнер");
		Запрос.УстановитьПараметр("Партнер", Партнер);
	КонецЕсли; 
    
    Запрос.Текст = ТекстЗапроса;

	Товары.Загрузить(Запрос.Выполнить().Выгрузить());

КонецПроцедуры

&НаСервере
Процедура ПривязатьСервер()
	
	Строки = Элементы.Товары.ВыделенныеСтроки;
	Для Каждого ТекСтр Из Строки Цикл
		СтрокаТЧ = Товары.НайтиПоИдентификатору(ТекСтр);
		ЗаписатьНомкенклатуруВБазу(СтрокаТЧ.НоменклатураПоставщика, СтрокаТЧ.Номенклатура);
		Товары.Удалить(СтрокаТЧ);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьПривязкуСервер()
	
	Для каждого Стр Из Товары Цикл
		ЗаписатьНомкенклатуруВБазу(Стр.НоменклатураПоставщика, Стр.Номенклатура);
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНомкенклатуруВБазу(НоменклатураПоставщика, Номенклатура)
	СпрОбъект = НоменклатураПоставщика.ПолучитьОбъект();
	СпрОбъект.Номенклатура = Номенклатура;
	СпрОбъект.Заполнить(Неопределено);
	СпрОбъект.Записать();
КонецПроцедуры

&НаКлиенте
Функция ПроверитьЗаполнениеНеобходимыхПолей()
	НеобходимыеПоляЗаполенены = Истина;
	Если ВРег(РежимПоиска) = ВРег("Содержит") Тогда
		Если Партнер.Пустая() Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru='Так как указан вид сравнения ""Содержит"" нужно указать Партнера.'");
			Сообщение.Поле = "Партнер";
			Сообщение.Сообщить();
		КонецЕсли;
		НеобходимыеПоляЗаполенены = Ложь; 
	КонецЕсли; 
	
	Если ПустаяСтрока(РежимПоиска) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Не указан ""Режим Поиска""'");
		Сообщение.Поле = "РежимПоиска";
		Сообщение.Сообщить();
		НеобходимыеПоляЗаполенены = Ложь; 
	КонецЕсли;
	
	Если ПустаяСтрока(ПолеНоменклатуры) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Не указано ""Поле номенклатуры""'");
		Сообщение.Поле = "ПолеНоменклатуры";
		Сообщение.Сообщить();
		НеобходимыеПоляЗаполенены = Ложь; 
	КонецЕсли;
	
	Если ПустаяСтрока(ПолеНоменклатурыПоставщика) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Не указано ""Поле номенклатуры поставщика""'");
		Сообщение.Поле = "ПолеНоменклатурыПоставщика";
		Сообщение.Сообщить();
		НеобходимыеПоляЗаполенены = Ложь; 
	КонецЕсли;

	Возврат НеобходимыеПоляЗаполенены;
КонецФункции
 
#КонецОбласти
