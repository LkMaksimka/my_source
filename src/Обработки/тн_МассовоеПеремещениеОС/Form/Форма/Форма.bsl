#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    Дата = ТекущаяДатаСеанса();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
    Если ИсточникВыбора.ИмяФормы = "Обработка.тн_МассовоеПеремещениеОС.Форма.ФормаЗаполнения" Тогда
        ТаблицаОС = ВыбранноеЗначение.ТаблицаОС;
        Для каждого Строка Из ТаблицаОС Цикл
            НоваяСтрока = Таблица.Добавить();
            ЗаполнениеСтрокиТаблицы(НоваяСтрока, Строка);
        КонецЦикла;
        НеНайденныеКоды = ВыбранноеЗначение.НеНайденныеКоды;
        Для каждого Элемент Из НеНайденныеКоды Цикл
        	ТекстСообщения = НСтр("ru = 'Для Кода ""%1"" не найдено Основное средство !'");
            ТекстСообщения = СтрШаблон(ТекстСообщения, Элемент);
            Сообщение = Новый СообщениеПользователю;
            Сообщение.Текст = ТекстСообщения;
            Сообщение.Сообщить();            
        КонецЦикла; 
    КонецЕсли; 
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АдресНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
    ЗаполнитьАдрес(АдресЗначение, Адрес, 
                    "АдресаНачалоВыбораЗавершение", Новый Соответствие);
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеПолучательПриИзменении(Элемент)
   КодБЕПолучателя = ПолучитьКодБЕ_Подразделения(ПодразделениеПолучатель);
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблица

&НаКлиенте
Процедура ТаблицаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
    
    Если ВыбранноеЗначение.Количество() > 0 Тогда 
        
        ТекСтрока = Элементы.Таблица.ТекущиеДанные;
        Если ТекСтрока <> Неопределено И ВыбранноеЗначение.Количество() = 1 Тогда
            ЗаполнениеСтрокиТаблицы(ТекСтрока, ВыбранноеЗначение[0]); 
        Иначе
            Для каждого ЭлементМассива Из ВыбранноеЗначение Цикл
                
                Отбор = Новый Структура;
                Отбор.Вставить("ОсновноеСредство", ЭлементМассива.ОсновноеСредство);
                Отбор.Вставить("ПодразделениеСдатчик", ЭлементМассива.ПодразделениеСдатчик);
                Отбор.Вставить("МОЛСдатчик", ЭлементМассива.МОЛСдатчик);
                НайденыСтроки = Таблица.НайтиСтроки(Отбор);
                
                Если НайденыСтроки.Количество() = 0 Тогда
                    НоваяСтрока = Таблица.Добавить();
                    ЗаполнениеСтрокиТаблицы(НоваяСтрока, ЭлементМассива);
                КонецЕсли;
                
            КонецЦикла;
            
        КонецЕсли; 
        
    Иначе
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = НСтр("ru='Нет выбранных данных.'");
        Сообщение.Сообщить();
    КонецЕсли; 
    
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаАдресПолучательНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
    СтандартнаяОбработка = Ложь;
    ТекСтрока = Элементы.Таблица.ТекущиеДанные;

    ДополнительныеПараметры = Новый Соответствие;
    ДополнительныеПараметры.Вставить("ТекСтрока",  ТекСтрока);
   	ЗаполнитьАдрес(ТекСтрока.АдресПолучательЗначение, ТекСтрока.АдресПолучатель, 
                    "АдресаДоставкиНачалоВыбораЗавершение", ДополнительныеПараметры);
  	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаОсновноеСредствоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
    СтандартнаяОбработка = Ложь;
    ОткрытьПодборОС(Ложь, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПодразделениеПолучательПриИзменении(Элемент)
    ТекДанные = Элементы.Таблица.ТекущиеДанные;
    Если ТекДанные <> Неопределено Тогда
        КодБЕ_Получатель = ПолучитьКодБЕ_Подразделения(ТекДанные.ПодразделениеПолучатель);
        ТекДанные.КодБЕ_ПодразделениеПолучатель = КодБЕ_Получатель;
    КонецЕсли; 
    
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подобрать(Команда)
    ОткрытьПодборОС(Истина, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
   ПараметрыФормы = Новый Структура;
   ПараметрыФормы.Вставить("Дата", КонецДня(Дата));
   ПараметрыФормы.Вставить("Организация", Организация);
   
   ОткрытьФорму("Обработка.тн_МассовоеПеремещениеОС.Форма.ФормаЗаполнения", 
                ПараметрыФормы, 
                ЭтаФорма,
                УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументы(Команда)
	Сообщение = Новый СообщениеПользователю;
	
	Для Каждого Строка Из Таблица Цикл
        
        ЗаполненыНужныеПоля = ОбязательныеПоляЗаполнены(Строка);                  
                            
        Если НЕ ЗаполненыНужныеПоля Тогда
            Сообщение.Текст = НСтр("ru='НЕ все поля заполнены'");
            Сообщение.Сообщить();
            Возврат;
        КонецЕсли;
        
    КонецЦикла;
    
    ОшибкиПриСозданиДокумента = СоздатьДокументыНаСервере(Таблица, Организация, Дата);
    Для каждого Элемент Из ОшибкиПриСозданиДокумента Цикл
        Сообщение.Текст = Элемент;
        Сообщение.Сообщить();
	КонецЦикла; 
	
	Сообщение.Текст = НСтр("ru='Документы созданы.'");
    Сообщение.Сообщить();

КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКСпискуДокументов(Команда)
    
    ПараметрыФормы = Новый Структура;
    ПараметрыФормы.Вставить("тн_Ответственный", ПолучитьТекущегоПользователя());
    ПараметрыФормы.Вставить("тн_Дата", Дата);
    
    Форма = ОткрытьФорму("Документ.ПеремещениеОС2_4.ФормаСписка", ПараметрыФормы);
    Форма.Открыть();
    
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура АдресаДоставкиНачалоВыбораЗавершение(Знач РезультатЗакрытия, Знач ДополнительныеПараметры) Экспорт
	Если РезультатЗакрытия <> Неопределено Тогда
		ТекСтрока = ДополнительныеПараметры.ТекСтрока;
		ТекСтрока.АдресПолучательЗначение = РезультатЗакрытия.Значение;
		ТекСтрока.АдресПолучатель = РезультатЗакрытия.Представление;
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура АдресаНачалоВыбораЗавершение(Знач РезультатЗакрытия, Знач ДополнительныеПараметры) Экспорт
	Если РезультатЗакрытия <> Неопределено Тогда
		АдресЗначение = РезультатЗакрытия.Значение;
		Адрес = РезультатЗакрытия.Представление;
	КонецЕсли; 
КонецПроцедуры

&НаСервереБезКонтекста
Функция СоздатьДокументыНаСервере(Знач Таблица, Знач Организация, Дата)
    
    ДокОбъект = Неопределено;
    Ошибки = Новый Массив;
    
    Для каждого Строка Из Таблица Цикл
    	        
        НуженНовыйДокумент = СоздатьНовыйДокумент(ДокОбъект, Строка);
        Если НуженНовыйДокумент Тогда
            Если ДокОбъект <> Неопределено Тогда
                ЗаписатьДокумент(ДокОбъект, Ошибки);
            КонецЕсли; 
        	ДокОбъект = Документы.ПеремещениеОС2_4.СоздатьДокумент();
            ДокОбъект.Дата = Дата;
            ДокОбъект.Организация = Организация;
            ДокОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеОС;
            ДокОбъект.Ответственный = Пользователи.ТекущийПользователь();
            Комментарий = НСтр("ru = '# Автоматически создано обработкой ""Массовое Перемещение(ОС)"" от %1.'");
            Комментарий = СтрШаблон(Комментарий, Формат(ДокОбъект.Дата, "ДЛФ=DDT"));
            ДокОбъект.Комментарий = Комментарий;
        
            ДокОбъект.Подразделение =  Строка.ПодразделениеСдатчик;
            ДокОбъект.МОЛ =  Строка.МОЛСдатчик;            
            ЗаполнитьЗначенияСвойств(ДокОбъект, Строка);
        КонецЕсли; 
         НовСтр = ДокОбъект.ОС.Добавить();
         НовСтр.ОсновноеСредство =  Строка.ОсновноеСредство;
    КонецЦикла;     
    
    Если ДокОбъект <> Неопределено Тогда
        ЗаписатьДокумент(ДокОбъект, Ошибки);
    КонецЕсли; 

    Возврат Ошибки;
КонецФункции

&НаСервереБезКонтекста
Функция СоздатьНовыйДокумент(Знач ДокОбъект, Знач Строка)
    Если ДокОбъект = Неопределено Тогда
    	 НуженНовыйДокумент = Истина;
     Иначе
         НуженНовыйДокумент = СравнитьПоля(ДокОбъект, Строка);
    КонецЕсли; 
    
    Возврат НуженНовыйДокумент;
КонецФункции 

&НаСервереБезКонтекста
Функция СравнитьПоля(ДокОбъект, Строка)
    НуженНовыйДокумент = НЕ (ДокОбъект.Подразделение =  Строка.ПодразделениеСдатчик
                         И ДокОбъект.МОЛ =  Строка.МОЛСдатчик
                         И ДокОбъект.ПодразделениеПолучатель =  Строка.ПодразделениеПолучатель
                         И ДокОбъект.МОЛПолучатель =  Строка.МОЛПолучатель
                         И ДокОбъект.НачислениеАмортизации =  Строка.НачислениеАмортизации
                         И ДокОбъект.СтатьяРасходов =  Строка.СтатьяРасходов
                         И ДокОбъект.АналитикаРасходов =  Строка.АналитикаРасходов
                         И ДокОбъект.ИзменяетсяОтражениеРасходовПоНалогу =  Строка.ИзменяетсяОтражениеРасходовПоНалогу
                         И ДокОбъект.СтатьяРасходовНалог =  Строка.СтатьяРасходовНалог
                         И ДокОбъект.АналитикаРасходовНалог =  Строка.АналитикаРасходовНалог);
    Возврат НуженНовыйДокумент;         
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаписатьДокумент(Знач ДокОбъект, Ошибки)

    Попытка
        ДокОбъект.Заполнить(Неопределено);
        ДокОбъект.Записать(РежимЗаписиДокумента.Проведение);  
        Успешно = Истина;
    Исключение
        ДокОбъект.Записать(РежимЗаписиДокумента.Запись);  
        
        ТекстОшибки = ОписаниеОшибки();
        Ошибки.Добавить(ТекстОшибки);
        
        Успешно = Ложь;
	КонецПопытки;
	
    Если Успешно Тогда
        ДопРеквизит_ОКВЭД = "ОКВЭД_b2cd3d401b88498cafc8a1e91c334a60";
        
        // Заполняем доп. реквизит ОКВЭД
        ОКВЭД = УправлениеСвойствами.ЗначениеСвойства(ДокОбъект.Подразделение, ДопРеквизит_ОКВЭД);
        Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", ДопРеквизит_ОКВЭД); 
        ТипСвойства = Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения");
        ТаблицаСвойствИЗначений = Новый ТаблицаЗначений;
        ТаблицаСвойствИЗначений.Колонки.Добавить("Свойство", ТипСвойства);
        ТаблицаСвойствИЗначений.Колонки.Добавить("Значение");
        НовСтр = ТаблицаСвойствИЗначений.Добавить();
        НовСтр.Свойство = Свойство;
        НовСтр.Значение = ОКВЭД;
        Для Каждого ОС Из ДокОбъект.ОС Цикл
            УправлениеСвойствами.ЗаписатьСвойстваУОбъекта(ОС.ОсновноеСредство, ТаблицаСвойствИЗначений);
        КонецЦикла;
        
    КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПодборОС(МножественныйВыбор, ЗакрыватьПриВыборе)
    
    Если Организация.Пустая() Тогда
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = НСтр("ru='Поле ""Организация"" не заполнено'");
        Сообщение.Поле = "Организация";
        Сообщение.Сообщить();
        
        Возврат ;
    КонецЕсли; 
 
    
   ПараметрыФормы = Новый Структура;
   ПараметрыФормы.Вставить("Дата", КонецДня(Дата));
   ПараметрыФормы.Вставить("Организация", Организация);
   ПараметрыФормы.Вставить("МножественныйВыбор", МножественныйВыбор);
   ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", ЗакрыватьПриВыборе);
   
   ОткрытьФорму("Обработка.тн_МассовоеПеремещениеОС.Форма.ФормаПодбора", 
					ПараметрыФормы, Элементы.Таблица, , , , , 
					РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры
                
&НаКлиенте
Функция ОбязательныеПоляЗаполнены(Строка)
    ЗаполненыНужныеПоля = ЗначениеЗаполнено(Строка.ОсновноеСредство)
                        И ЗначениеЗаполнено(Строка.ПодразделениеПолучатель)
                        И ЗначениеЗаполнено(Строка.МОЛПолучатель)
                        И ЗначениеЗаполнено(Строка.СтатьяРасходовНалог)
                        И ЗначениеЗаполнено(Строка.СтатьяРасходов); 
                        
    Возврат  ЗаполненыНужныеПоля;
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьКодБЕ_Подразделения(Подразделение)
    КодБЕ = Неопределено; 
    
    Запрос = Новый Запрос;
    Запрос.Текст = 
    "ВЫБРАТЬ
     |  СтруктураПредприятияДополнительныеРеквизиты.Ссылка КАК Ссылка,
     |  СтруктураПредприятияДополнительныеРеквизиты.Значение КАК Значение
     |ИЗ
     |  Справочник.СтруктураПредприятия.ДополнительныеРеквизиты КАК СтруктураПредприятияДополнительныеРеквизиты
     |      ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
     |      ПО СтруктураПредприятияДополнительныеРеквизиты.Свойство = ДополнительныеРеквизитыИСведения.Ссылка
     |          И (ДополнительныеРеквизитыИСведения.Имя = &ИмяКодБЕ)
     |ГДЕ
     |  СтруктураПредприятияДополнительныеРеквизиты.Ссылка = &Ссылка";
    Запрос.УстановитьПараметр("Ссылка", Подразделение);
    Запрос.УстановитьПараметр("ИмяКодБЕ", "БЕ_SAP");
    
    РезультатЗапроса = Запрос.Выполнить();
    Если НЕ РезультатЗапроса.Пустой() Тогда
        Выборка = РезультатЗапроса.Выбрать();
        Выборка.Следующий();
        КодБЕ = Выборка.Значение;
    КонецЕсли; 
    
    Возврат КодБЕ;
КонецФункции

&НаКлиенте
Процедура ЗаполнитьАдрес(ЗначенияПолей, Представление, ИмяПроцедурыОповещения, ДополнительныеПараметры)
    
    ВидКонтактнойИнформации = ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ПочтовыйАдресОрганизации");
    
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("ВидКонтактнойИнформации", ВидКонтактнойИнформации); 
                                    
	ПараметрыОткрытияФормы.Вставить("Заголовок", нСтр("ru='Адрес'", "ru"));
	ПараметрыОткрытияФормы.Вставить("ЗначенияПолей", ЗначенияПолей);
	ПараметрыОткрытияФормы.Вставить("Представление", Представление);
	
	Оповещение = Новый ОписаниеОповещения(ИмяПроцедурыОповещения, ЭтотОбъект, Новый Структура);
    Для каждого Строка Из ДополнительныеПараметры Цикл
    	Оповещение.ДополнительныеПараметры.Вставить(Строка.Ключ, Строка.Значение);
    КонецЦикла; 
    	
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуКонтактнойИнформации(ПараметрыОткрытияФормы, , Оповещение);

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКолонкиПолучателяПриИзменении(Элемент)
    Элементы.Получатель.Видимость = ПоказатьКолонкиПолучателя;
	Элементы.Группа3.Видимость 	  = НЕ ПоказатьКолонкиПолучателя;
	Если ПоказатьКолонкиПолучателя Тогда
		ПодразделениеПолучатель = ПредопределенноеЗначение("Справочник.СтруктураПредприятия.ПустаяСсылка");
		МОЛПолучатель		    = ПредопределенноеЗначение("Справочник.ФизическиеЛица.ПустаяСсылка"); 
		Адрес					= "";
		АдресЗначение			= "";
		КодБЕПолучателя			= Неопределено;
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнениеСтрокиТаблицы(НоваяСтрока, ЭлементМассива)
    ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементМассива);
    Если НЕ ПоказатьКолонкиПолучателя Тогда
        НоваяСтрока.АдресПолучатель = Адрес;
        НоваяСтрока.АдресПолучательЗначение = АдресЗначение;	
        НоваяСтрока.ПодразделениеПолучатель = ПодразделениеПолучатель;
        НоваяСтрока.МОЛПолучатель = МОЛПолучатель;
        НоваяСтрока.КодБЕ_ПодразделениеПолучатель = КодБЕПолучателя;
    КонецЕсли; 
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьТекущегоПользователя()
    Возврат ПараметрыСеанса.ТекущийПользователь;
КонецФункции

#КонецОбласти

