
#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ,  СтандартнаяОбработка)
	ЕдиницыИзмерения = Константы.ЕдиницаИзмеренияКоличестваШтук.Получить();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	Если ИсточникВыбора.ИмяФормы = "Справочник.Номенклатура.Форма.ФормаВыбораГруппы" Тогда
		НоменклатурнаяГруппа = ВыбранноеЗначение;
	КонецЕсли; 
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПоставщикПриИзменении(Элемент)
	ЗаполнитьТаблицуНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВидНоменклатурыПриИзменении(Элемент)
	НоменклатурнаяГруппа = ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка");
КонецПроцедуры

&НаКлиенте
Процедура НоменклатурнаяГруппаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	кв_ГруппыНоменклатуры = кв_ПолучитьГруппуПоПравиламЗаполнения(ВидНоменклатуры);
	Если кв_ГруппыНоменклатуры.Количество() > 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("кв_Отбор", кв_ГруппыНоменклатуры);
		ОткрытьФорму("Справочник.Номенклатура.ФормаВыбораГруппы", СтруктураОтбора, ЭтаФорма);
		
	КонецЕсли; 

КонецПроцедуры


#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда)
	ЗаполнитьТаблицуНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНоменклатуру(Команда)
	
	МассивПараметровСоздания = Новый Массив;
	ВыделенныеСтроки = Элементы.Таблица.ВыделенныеСтроки;
	
	Для каждого ИдСтроки Из ВыделенныеСтроки Цикл
		Строка = Таблица.НайтиПоИдентификатору(ИдСтроки);
		Если НЕ Строка.НоменклатураГруппа.Пустая() 
			И НЕ Строка.ВидНоменклатуры.Пустая() 
			И НЕ Строка.Производитель.Пустая() Тогда
			Структура = Новый Структура;
			Структура.Вставить("ИдСтроки",  ИдСтроки);
			Структура.Вставить("НоменклатураПоставщика",  Строка.НоменклатураПоставщиков);
			Структура.Вставить("Наименование",  Строка.Наименование);
			Структура.Вставить("КодПроизводителя",  Строка.КодПроизводителя);
			Структура.Вставить("кв_Вес", Строка.кв_Вес);
			Структура.Вставить("кв_Объем", Строка.кв_Объем);
			Структура.Вставить("ВидНоменклатуры", Строка.ВидНоменклатуры);
			Структура.Вставить("НоменклатураГруппа", Строка.НоменклатураГруппа);
			Структура.Вставить("Производитель", Строка.Производитель);
			Структура.Вставить("СозданИПривязан", Строка.СозданИПривязан);
			МассивПараметровСоздания.Добавить(Структура);
		КонецЕсли; 
	КонецЦикла; 
	
	СоздатьНоменклатуруНаСервере(МассивПараметровСоздания, ЕдиницыИзмерения);
	
	Для каждого ЭлементМасс Из МассивПараметровСоздания  Цикл
		Строка = Таблица.НайтиПоИдентификатору(ЭлементМасс.ИдСтроки);
		Строка.СозданИПривязан = ЭлементМасс.СозданИПривязан;
	КонецЦикла; 
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьВыделенные(Команда)
	ВыделенныеСтроки = Элементы.Таблица.ВыделенныеСтроки;
	Для каждого ИдСтроки Из ВыделенныеСтроки Цикл
		Строка = Таблица.НайтиПоИдентификатору(ИдСтроки);
		Строка.ВидНоменклатуры = ВидНоменклатуры;
		Строка.НоменклатураГруппа = НоменклатурнаяГруппа;
		Строка.Производитель = Производитель;
	КонецЦикла; 
КонецПроцедуры

&НаКлиенте
Процедура ОтфильтроватьПоПроизводителю(Команда)
	ЗаполнитьТаблицуНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицуНаСервере()
	
	Таблица.Очистить();
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	НоменклатураПоставщиков.Ссылка КАК НоменклатураПоставщиков,
	|	НоменклатураПоставщиков.кв_Объем КАК кв_Объем,
	|	НоменклатураПоставщиков.кв_Вес КАК кв_Вес,
	|	НоменклатураПоставщиков.КодПроизводителя КАК КодПроизводителя,
	|	НоменклатураПоставщиков.Артикул КАК Артикул,
	|	НоменклатураПоставщиков.Наименование КАК Наименование
	|ПОМЕСТИТЬ ВременнаяТаблица
	|ИЗ
	|	Справочник.НоменклатураПоставщиков КАК НоменклатураПоставщиков
	|ГДЕ
	|	НЕ НоменклатураПоставщиков.ЭтоГруппа
	|	И НЕ НоменклатураПоставщиков.ПометкаУдаления
	|	И НоменклатураПоставщиков.Владелец = &Поставщик
	|	И НоменклатураПоставщиков.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	И НЕ НоменклатураПоставщиков.Некондиция
	|	И НоменклатураПоставщиков.КодПроизводителя <> """"
	|	И НоменклатураПоставщиков.Наименование ПОДОБНО &Производитель
	|";
	
	Если ВыводитьПоНаличиюУПоставщика Тогда
		ТекстЗапроса  = ТекстЗапроса +
		"
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблица.НоменклатураПоставщиков КАК НоменклатураПоставщиков, 
		|	ВременнаяТаблица.кв_Объем КАК кв_Объем, 
		|	ВременнаяТаблица.кв_Вес КАК кв_Вес, 
		|	ВременнаяТаблица.КодПроизводителя КАК КодПроизводителя, 
		|	ВременнаяТаблица.Артикул КАК Артикул, 
		|	ВременнаяТаблица.Наименование КАК Наименование, 
		|	ЕСТЬNULL(кв_НаличиеНомеклатурыПоставщика.ДатаЗагрузкиПрайса,  0) КАК ДатаЗагрузкиПрайса
		|ИЗ
		|	ВременнаяТаблица КАК ВременнаяТаблица
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.кв_НаличиеНомеклатурыПоставщика КАК кв_НаличиеНомеклатурыПоставщика
		|		ПО ВременнаяТаблица.НоменклатураПоставщиков = кв_НаличиеНомеклатурыПоставщика.НоменклатураПоставщиков
		|			И (кв_НаличиеНомеклатурыПоставщика.ЕстьВНаличие)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблица.НоменклатураПоставщиков, 
		|	ВременнаяТаблица.кв_Объем, 
		|	ВременнаяТаблица.кв_Вес, 
		|	ВременнаяТаблица.КодПроизводителя, 
		|	ВременнаяТаблица.Артикул, 
		|	ВременнаяТаблица.Наименование, 
		|	кв_НаличиеНомеклатурыПоставщика.ДатаЗагрузкиПрайса
		|
		|ИМЕЮЩИЕ
		|	кв_НаличиеНомеклатурыПоставщика.ДатаЗагрузкиПрайса <> 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	Наименование";
		
	Иначе 
		ТекстЗапроса  = ТекстЗапроса +
		"
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблица.НоменклатураПоставщиков КАК НоменклатураПоставщиков,
		|	ВременнаяТаблица.кв_Объем КАК кв_Объем,
		|	ВременнаяТаблица.кв_Вес КАК кв_Вес,
		|	ВременнаяТаблица.КодПроизводителя КАК КодПроизводителя,
		|	ВременнаяТаблица.Артикул КАК Артикул,
		|	ВременнаяТаблица.Наименование КАК Наименование
		|ИЗ
		|	ВременнаяТаблица КАК ВременнаяТаблица
		|
		|УПОРЯДОЧИТЬ ПО
		|	Наименование";
	КонецЕсли; 

	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Поставщик",  Поставщик);
	Запрос.УстановитьПараметр("Производитель",  "%" + Производитель + "%");
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	НомерСтроки = 0;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		НовСтр = Таблица.Добавить();
		НомерСтроки = НомерСтроки + 1;
		ЗаполнитьЗначенияСвойств(НовСтр, ВыборкаДетальныеЗаписи);
		НовСтр.НомерСтроки = НомерСтроки;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СоздатьНоменклатуруНаСервере(МассивПараметровСоздания, ЕдиницыИзмерения)
	
	Если МассивПараметровСоздания.Количество() = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	
	Для каждого ЭлементМасс Из МассивПараметровСоздания Цикл
		// проверка,  есть ли у номенклатуры поставщика привязанная наша номенклатура.
		Если НЕ ЭлементМасс.НоменклатураПоставщика.Номенклатура.Пустая() Тогда
			Продолжить;
		КонецЕсли; 
		
		СпрНоваяНоменклатура = Справочники.Номенклатура.СоздатьЭлемент();
		СпрНоваяНоменклатура.Наименование = ЭлементМасс.Наименование;
		СпрНоваяНоменклатура.КодПроизводителя = ЭлементМасс.КодПроизводителя;
		СпрНоваяНоменклатура.ВидНоменклатуры = ЭлементМасс.ВидНоменклатуры;
		СпрНоваяНоменклатура.Родитель = ЭлементМасс.НоменклатураГруппа;
		СпрНоваяНоменклатура.ЕдиницаИзмерения = ЕдиницыИзмерения;
		СпрНоваяНоменклатура.Производитель = ЭлементМасс.Производитель;
		СпрНоваяНоменклатура.Заполнить(Неопределено);
		
		Если ЭлементМасс.кв_Вес <> 0 Тогда
			СпрНоваяНоменклатура.ВесИспользовать = Истина;
			СпрНоваяНоменклатура.ВесЧислитель 	 = ЭлементМасс.кв_Вес;
			СпрНоваяНоменклатура.ВесЗнаменатель	 = 1;	
		КонецЕсли; 
		
		Если ЭлементМасс.кв_Объем <> 0 Тогда
			СпрНоваяНоменклатура.ОбъемИспользовать = Истина;
			СпрНоваяНоменклатура.ОбъемЧислитель   = ЭлементМасс.кв_Объем;
			СпрНоваяНоменклатура.ОбъемЗнаменатель = 1;	
		КонецЕсли; 
		
		
		СпрНоваяНоменклатура.ОбменДанными.Загрузка = Истина;
		Если Не ЗначениеЗаполнено(СпрНоваяНоменклатура.Код) Тогда
			СпрНоваяНоменклатура.УстановитьНовыйКод();
		КонецЕсли;
		СпрНоваяНоменклатура.Записать();
		
		ОбъектНомПоставщика = ЭлементМасс.НоменклатураПоставщика.ПолучитьОбъект();
		ОбъектНомПоставщика.Номенклатура = СпрНоваяНоменклатура.Ссылка;
		ОбъектНомПоставщика.Заполнить(Неопределено);
		ОбъектНомПоставщика.Записать();
		ЭлементМасс.СозданИПривязан = Истина;
	КонецЦикла; 
	
КонецПроцедуры // *СоздатьНоменклатуруНаСервере(

&НаСервереБезКонтекста
Функция кв_ПолучитьГруппуПоПравиламЗаполнения(кв_ВидНоменклатуры)
	кв_ГруппыНоменклатуры = РегистрыСведений.кв_ПравилаУказанияГруппыДляНоменклатуры.ПолучитьГруппыНоменклатуры(кв_ВидНоменклатуры);
	Возврат кв_ГруппыНоменклатуры; 
КонецФункции

#КонецОбласти

