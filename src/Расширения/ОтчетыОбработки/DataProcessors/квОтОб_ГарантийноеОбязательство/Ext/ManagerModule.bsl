#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Определяет состав программного интерфейса для интеграции с конфигурацией.
//
// Параметры:
//   Настройки - Структура - Настройки интеграции этого объекта.
//       См. возвращаемое значение функции ПодключаемыеКоманды.НастройкиПодключаемыхОтчетовИОбработок().
//
Процедура ПриОпределенииНастроек(Настройки) Экспорт
	Настройки.ДобавитьКомандыПечати = Истина;
	Настройки.Размещение.Добавить(Метаданные.Документы.РеализацияТоваровУслуг);
	Настройки.Размещение.Добавить(Метаданные.Документы.РасходныйОрдерНаТовары);
КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	Команда = КомандыПечати.Добавить();
	Команда.Представление = НСтр("ru = '(кв) Гарантийное обязательство(из расширения)'");
	Команда.Идентификатор = "кв_ГарантийноеОбязательство";
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//   МассивОбъектов  - Массив    - Ссылки на объекты, которые нужно распечатать.
//   ПараметрыПечати - Структура - Дополнительные настройки печати.
//   КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр).
//   ОбъектыПечати - СписокЗначений - Печатаемые объекты.
//       Значение - Ссылка на объект.
//       Представление - Имя области в которой был выведен объект (выходной параметр).
//   ПараметрыВывода - Структура - Дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	ПолноеИмя = Создать().Метаданные().ПолноеИмя();
	
	Если МассивОбъектов.Количество() > 1 Тогда
		ВызватьИсключение нСтр("ru='Доступна печать только одного документа. 
							    |Пожалуйста выберите только один документ.'", "ru");
	КонецЕсли; 
	
	Если ТипЗнч(МассивОбъектов[0]) = Тип("ДокументСсылка.РеализацияТоваровУслуг")  Тогда
		ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "кв_ГарантийноеОбязательство");
		Если ПечатнаяФорма <> Неопределено Тогда
			ПечатнаяФорма.ТабличныйДокумент = ПечатьГарантийноеОбязательство(МассивОбъектов, ОбъектыПечати, ПолноеИмя);
			ПечатнаяФорма.СинонимМакета = НСтр("ru = ''(кв) Гарантийное обязательство (подключенная печатная форма)'");
			ПечатнаяФорма.ПолныйПутьКМакету = ПолноеИмя + ".ПФ_MXL_ГарантийноеОбязательство";
		КонецЕсли;	
		
	ИначеЕсли ТипЗнч(МассивОбъектов[0]) = Тип("ДокументСсылка.РасходныйОрдерНаТовары") Тогда
		ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "кв_ГарантийноеОбязательство");
		Если ПечатнаяФорма <> Неопределено Тогда
			ПечатнаяФорма.ТабличныйДокумент = ПечатьГарантийногоОбязательстваРасходногоОрдера(МассивОбъектов, 
																								ОбъектыПечати, 
																								ПолноеИмя);
			ПечатнаяФорма.СинонимМакета = НСтр("ru = ''(кв) Гарантийное обязательство (подключенная печатная форма)'");
			ПечатнаяФорма.ПолныйПутьКМакету = ПолноеИмя + ".ПФ_MXL_ГарантийноеОбязательство";
		КонецЕсли;	
	Иначе
		ВызватьИсключение нСтр("ru='Не поддерживаемы Документ!!!'", "ru");
	КонецЕсли; 
	
	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура печати документа.
//
Функция ПечатьГарантийноеОбязательство(МассивОбъектов, ОбъектыПечати, ПолноеИмя) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_РеализацияТоваровУслуг_кв_ГарантийноеОбязательство";
	
	ПВХСсылка = ПолучитьСсылкуНаДополнительныйРеквизитГарантии();
	
	ЗапросПоСкладам = Новый Запрос;
	ЗапросПоСкладам.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацияТоваровУслугТовары.Ссылка КАК Ссылка,
	|	РеализацияТоваровУслугТовары.Склад КАК Склад,
	|	РеализацияТоваровУслугТовары.Склад.ИспользоватьСерииНоменклатуры КАК ИспользоватьСерииНоменклатуры
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|ГДЕ
	|	РеализацияТоваровУслугТовары.Ссылка В(&МассивОбъектов)
	|	И РеализацияТоваровУслугТовары.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|ИТОГИ ПО
	|	ИспользоватьСерииНоменклатуры";
	
	ЗапросПоСкладам.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	// Создание массива для проверки: какие из документов не попали в выборку
	МассивПроверки = Новый Массив;
	Для Каждого Ссылка Из МассивОбъектов Цикл
		МассивПроверки.Добавить(Ссылка);
	КонецЦикла;
	
	ДеревоОбъектов = ЗапросПоСкладам.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
		
	Для Каждого ИспользованиеСерий Из ДеревоОбъектов.Строки Цикл
		
		ЗапросПоТоварам = Новый Запрос;
		ЗапросПоТоварам.УстановитьПараметр("МассивОбъектов", ИспользованиеСерий.Строки.ВыгрузитьКолонку("Ссылка"));
		ЗапросПоТоварам.УстановитьПараметр("МассивСкладов",  ИспользованиеСерий.Строки.ВыгрузитьКолонку("Склад"));
		ЗапросПоТоварам.УстановитьПараметр("Свойство",  ПВХСсылка);
		
		Если ИспользованиеСерий.ИспользоватьСерииНоменклатуры Тогда
			
			ТекстЗапросаПоТоварам = 
			"ВЫБРАТЬ
			|	РеализацияТоваровУслугТовары.Ссылка КАК Ссылка,
			|	РеализацияТоваровУслугТовары.Склад КАК Склад,
			|	РеализацияТоваровУслугТовары.КоличествоУпаковок КАК КоличествоУпаковок,
			|	РеализацияТоваровУслугТовары.Количество КАК Количество,
			|	РеализацияТоваровУслугТовары.Номенклатура КАК Номенклатура,
			|	РеализацияТоваровУслугТовары.Характеристика КАК Характеристика,
			|	РеализацияТоваровУслугТовары.Упаковка КАК Упаковка,
			|	"""" КАК Серия,
			|	ЕСТЬNULL(НоменклатураДополнительныеРеквизиты.Значение, 0) КАК Гарантия
			|ПОМЕСТИТЬ ТаблицаТоваров
			|ИЗ
			|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
			|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура.ДополнительныеРеквизиты КАК НоменклатураДополнительныеРеквизиты
			|			ПО (РеализацияТоваровУслугТовары.Номенклатура = НоменклатураДополнительныеРеквизиты.Ссылка)
			|				И (НоменклатураДополнительныеРеквизиты.Свойство = &Свойство)
			|ГДЕ
			|	РеализацияТоваровУслугТовары.Ссылка В(&МассивОбъектов)
			|	И РеализацияТоваровУслугТовары.Склад В(&МассивСкладов)
			|	И НЕ(РеализацияТоваровУслугТовары.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
			|				И РеализацияТоваровУслугТовары.СтатусУказанияСерий В (2, 4, 6, 8))
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	РеализацияТоваровУслугСерии.Ссылка,
			|	РеализацияТоваровУслугСерии.Склад,
			|	РеализацияТоваровУслугСерии.Количество,
			|	РеализацияТоваровУслугСерии.Количество,
			|	РеализацияТоваровУслугСерии.Номенклатура,
			|	РеализацияТоваровУслугСерии.Характеристика,
			|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка),
			|	РеализацияТоваровУслугСерии.Серия,
			|	"""" КАК Гарантия
			|ИЗ
			|	Документ.РеализацияТоваровУслуг.Серии КАК РеализацияТоваровУслугСерии
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК ТаблицаТовары
			|		ПО РеализацияТоваровУслугСерии.Ссылка = ТаблицаТовары.Ссылка
			|			И РеализацияТоваровУслугСерии.Номенклатура = ТаблицаТовары.Номенклатура
			|			И РеализацияТоваровУслугСерии.Характеристика = ТаблицаТовары.Характеристика
			|			И РеализацияТоваровУслугСерии.Склад = ТаблицаТовары.Склад
			|ГДЕ
			|	РеализацияТоваровУслугСерии.Ссылка В(&МассивОбъектов)
			|	И РеализацияТоваровУслугСерии.Склад В(&МассивСкладов)
			|	И РеализацияТоваровУслугСерии.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
			|
			|СГРУППИРОВАТЬ ПО
			|	РеализацияТоваровУслугСерии.Ссылка,
			|	РеализацияТоваровУслугСерии.Склад,
			|	РеализацияТоваровУслугСерии.Номенклатура,
			|	РеализацияТоваровУслугСерии.Характеристика,
			|	РеализацияТоваровУслугСерии.Серия,
			|	РеализацияТоваровУслугСерии.Количество,
			|	РеализацияТоваровУслугСерии.Количество
			|
			|ИМЕЮЩИЕ
			|	МАКСИМУМ(ТаблицаТовары.СтатусУказанияСерий) В (2, 4, 6, 8, 10)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаТоваров.Ссылка,
			|	ТаблицаТоваров.Склад,
			|	СУММА(ТаблицаТоваров.КоличествоУпаковок) КАК КоличествоУпаковок,
			|	СУММА(ТаблицаТоваров.Количество) КАК Количество,
			|	ТаблицаТоваров.Номенклатура,
			|	ТаблицаТоваров.Характеристика,
			|	ТаблицаТоваров.Упаковка,
			|	ВЫБОР
			|		КОГДА ТаблицаТоваров.Номенклатура.ВидНоменклатуры.НастройкаИспользованияСерий = ЗНАЧЕНИЕ(Перечисление.НастройкиИспользованияСерийНоменклатуры.ЭкземплярТовара)
			|			ТОГДА NULL
			|		ИНАЧЕ ТаблицаТоваров.Серия
			|	КОНЕЦ КАК Серия,
			|   ТаблицаТоваров.Гарантия
			|ПОМЕСТИТЬ СуммированнаяТаблицаТоваров
			|ИЗ
			|	ТаблицаТоваров КАК ТаблицаТоваров
			|
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаТоваров.Ссылка,
			|	ТаблицаТоваров.Склад,
			|	ТаблицаТоваров.Номенклатура,
			|	ТаблицаТоваров.Характеристика,
			|	ТаблицаТоваров.Упаковка,
			|	ВЫБОР
			|		КОГДА ТаблицаТоваров.Номенклатура.ВидНоменклатуры.НастройкаИспользованияСерий = ЗНАЧЕНИЕ(Перечисление.НастройкиИспользованияСерийНоменклатуры.ЭкземплярТовара)
			|			ТОГДА NULL
			|		ИНАЧЕ ТаблицаТоваров.Серия
			|	КОНЕЦ,
			|   ТаблицаТоваров.Гарантия
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаТоваров.Ссылка КАК Ссылка,
			|	ТаблицаТоваров.Ссылка.Организация КАК Организация,
			|	ТаблицаТоваров.Ссылка.Контрагент КАК Контрагент,
			|	ТаблицаТоваров.Ссылка.Номер КАК Номер,
			|	ТаблицаТоваров.Ссылка.Дата КАК Дата,
			|	ТаблицаТоваров.Ссылка.Организация.Префикс КАК Префикс,
			|	ТаблицаТоваров.Склад КАК Склад,
			|	&ТекстЗапросаНаименованиеЕдиницыИзмерения КАК ПредставлениеЕдиницыИзмеренияУпаковки,
			|	ПРЕДСТАВЛЕНИЕ(ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения) КАК ПредставлениеБазовойЕдиницыИзмерения,
			|	ТаблицаТоваров.КоличествоУпаковок КАК КоличествоУпаковок,
			|	ТаблицаТоваров.Количество КАК Количество,
			|	ТаблицаТоваров.Номенклатура.ВидНоменклатуры.НастройкаИспользованияСерий КАК НастройкаИспользованияСерий,
			|	ТаблицаТоваров.Номенклатура.Код КАК Код,
			|	ТаблицаТоваров.Номенклатура.Артикул КАК Артикул,
			|	ТаблицаТоваров.Номенклатура.НаименованиеПолное КАК ПредставлениеНоменклатуры,
			|	ТаблицаТоваров.Характеристика.НаименованиеПолное КАК ПредставлениеХарактеристики,
			|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
			|	ТаблицаТоваров.Характеристика КАК Характеристика,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) = 1
			|			ТОГДА НЕОПРЕДЕЛЕНО
			|		ИНАЧЕ ТаблицаТоваров.Упаковка.Наименование
			|	КОНЕЦ КАК Упаковка,
			|	ТаблицаТоваров.Серия.Номер КАК ПредставлениеСерии,
			|   ТаблицаТоваров.Гарантия,
			|	ВЫБОР
			|		КОГДА ТаблицаТоваров.Ссылка.ВернутьМногооборотнуюТару
			|				И ТаблицаТоваров.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ЭтоВозвратнаяТара
			|ИЗ
			|	СуммированнаяТаблицаТоваров КАК ТаблицаТоваров
			|
			|УПОРЯДОЧИТЬ ПО
			|	Ссылка,
			|	Склад,
			|	Номенклатура,
			|	Характеристика
			|ИТОГИ ПО
			|	Ссылка,
			|	Склад,
			|	Номенклатура,
			|	Характеристика
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаТоваров.Ссылка КАК Ссылка,
			|	ТаблицаТоваров.Склад КАК Склад,
			|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
			|	ТаблицаТоваров.Характеристика КАК Характеристика,
			|	ТаблицаТоваров.Серия.Номер КАК ПредставлениеСерии,
			|   ТаблицаТоваров.Гарантия
			|ИЗ
			|	ТаблицаТоваров КАК ТаблицаТоваров
			|ГДЕ
			|	ТаблицаТоваров.Номенклатура.ВидНоменклатуры.НастройкаИспользованияСерий = ЗНАЧЕНИЕ(Перечисление.НастройкиИспользованияСерийНоменклатуры.ЭкземплярТовара)
			|	И НЕ ТаблицаТоваров.Серия ЕСТЬ NULL 
			|
			|УПОРЯДОЧИТЬ ПО
			|	Ссылка,
			|	Склад,
			|	Номенклатура,
			|	Характеристика,
			|	ПредставлениеСерии";
			
			ТекстЗапросаПоТоварам = СтрЗаменить(ТекстЗапросаПоТоварам, "&ТекстЗапросаКоэффициентУпаковки",
				Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
					"ТаблицаТоваров.Упаковка",
					"ТаблицаТоваров.Номенклатура"));
					
			ТекстЗапросаПоТоварам = СтрЗаменить(ТекстЗапросаПоТоварам, "&ТекстЗапросаНаименованиеЕдиницыИзмерения",
				Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
					"Наименование",
					"ТаблицаТоваров.Упаковка",
					"ТаблицаТоваров.Номенклатура"));
			
			ЗапросПоТоварам.Текст	 = ТекстЗапросаПоТоварам;
			РезультатЗапроса		 = ЗапросПоТоварам.ВыполнитьПакет();
			ДанныеПечати			 = РезультатЗапроса[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			ВыборкаПоСериям 		 = РезультатЗапроса[3].Выбрать();
			
		Иначе
			
			ТекстЗапросаПоТоварам = 
			"ВЫБРАТЬ
			|	РеализацияТоваровУслугТовары.Ссылка КАК Ссылка,
			|	РеализацияТоваровУслугТовары.Ссылка.Организация КАК Организация,
			|	РеализацияТоваровУслугТовары.Ссылка.Контрагент КАК Контрагент,
			|	РеализацияТоваровУслугТовары.Ссылка.Номер КАК Номер,
			|	РеализацияТоваровУслугТовары.Ссылка.Дата КАК Дата,
			|	РеализацияТоваровУслугТовары.Ссылка.Организация.Префикс КАК Префикс,
			|	РеализацияТоваровУслугТовары.Склад КАК Склад,
			|	&ТекстЗапросаНаименованиеЕдиницыИзмерения КАК ПредставлениеЕдиницыИзмеренияУпаковки,
			|	ПРЕДСТАВЛЕНИЕ(РеализацияТоваровУслугТовары.Номенклатура.ЕдиницаИзмерения) КАК ПредставлениеБазовойЕдиницыИзмерения,
			|	РеализацияТоваровУслугТовары.КоличествоУпаковок КАК КоличествоУпаковок,
			|	РеализацияТоваровУслугТовары.Количество КАК Количество,
			|	РеализацияТоваровУслугТовары.Номенклатура.Код КАК Код,
			|	РеализацияТоваровУслугТовары.Номенклатура.Артикул КАК Артикул,
			|	РеализацияТоваровУслугТовары.Номенклатура.НаименованиеПолное КАК ПредставлениеНоменклатуры,
			|	РеализацияТоваровУслугТовары.Характеристика.НаименованиеПолное КАК ПредставлениеХарактеристики,
			|	"""" КАК ПредставлениеСерии,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) = 1
			|			ТОГДА НЕОПРЕДЕЛЕНО
			|		ИНАЧЕ РеализацияТоваровУслугТовары.Упаковка.Наименование
			|	КОНЕЦ КАК Упаковка,
			|	ВЫБОР
			|		КОГДА РеализацияТоваровУслугТовары.Ссылка.ВернутьМногооборотнуюТару
			|				И РеализацияТоваровУслугТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ЭтоВозвратнаяТара,
			|	NULL КАК НастройкаИспользованияСерий,
			|	РеализацияТоваровУслугТовары.Номенклатура КАК Номенклатура,
			|	РеализацияТоваровУслугТовары.Характеристика КАК Характеристика,
			|	ЕСТЬNULL(НоменклатураДополнительныеРеквизиты.Значение, 0) КАК Гарантия
			|ИЗ
			|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура.ДополнительныеРеквизиты КАК НоменклатураДополнительныеРеквизиты
			|			ПО (РеализацияТоваровУслугТовары.Номенклатура = НоменклатураДополнительныеРеквизиты.Ссылка)
			|				И (НоменклатураДополнительныеРеквизиты.Свойство = &Свойство)
			|ГДЕ
			|	РеализацияТоваровУслугТовары.Ссылка В(&МассивОбъектов)
			|	И РеализацияТоваровУслугТовары.Склад В(&МассивСкладов)
			|	И РеализацияТоваровУслугТовары.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
			|
			|УПОРЯДОЧИТЬ ПО
			|	Ссылка,
			|	Склад,
			|	РеализацияТоваровУслугТовары.НомерСтроки
			|ИТОГИ ПО
			|	Ссылка,
			|	Склад,
			|	Номенклатура,
			|	Характеристика";
			
			ТекстЗапросаПоТоварам = СтрЗаменить(ТекстЗапросаПоТоварам, "&ТекстЗапросаКоэффициентУпаковки",
				Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
					"РеализацияТоваровУслугТовары.Упаковка",
					"РеализацияТоваровУслугТовары.Номенклатура"));
					
			ТекстЗапросаПоТоварам = СтрЗаменить(ТекстЗапросаПоТоварам, "&ТекстЗапросаНаименованиеЕдиницыИзмерения",
				Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
					"Наименование",
					"РеализацияТоваровУслугТовары.Упаковка",
					"РеализацияТоваровУслугТовары.Номенклатура"));
			
			ЗапросПоТоварам.Текст = ТекстЗапросаПоТоварам;
			РезультатДанныеПечати = ЗапросПоТоварам.Выполнить();
			ДанныеПечати = РезультатДанныеПечати.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
		КонецЕсли;
		
		ПервыйДокумент = Истина;
		
		Макет = УправлениеПечатью.МакетПечатнойФормы(ПолноеИмя + ".ПФ_MXL_ГарантийноеОбязательство");
		
		РеквизитыДокумента = Новый Структура("Номер, Дата, Префикс, Представление");

		Пока ДанныеПечати.Следующий() Цикл
			
			ИндексМассиваПроверки = МассивПроверки.Найти(ДанныеПечати.Ссылка);
			Если ИндексМассиваПроверки <> Неопределено Тогда
				МассивПроверки.Удалить(ИндексМассиваПроверки);
			КонецЕсли;
			
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
			
			ЗаполнитьЗначенияСвойств(РеквизитыДокумента, ДанныеПечати);
			
			ВыборкаПоСкладам = ДанныеПечати.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаПоСкладам.Следующий() Цикл
				
				Если Не ПервыйДокумент Тогда
					ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				КонецЕсли;
				
				ПервыйДокумент = Ложь;
				
				Область = Макет.ПолучитьОбласть("Шапка");
				
				Заголовок = ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(РеквизитыДокумента, 
																						  НСтр("ru='Гарантийное обязательство'"));
																						  
				ОписаниеОрганизации =  ПолучитьОписаниеОЮрФизЛице(ВыборкаПоСкладам.Организация, ВыборкаПоСкладам.Дата);
				ОписаниеКонтрагента =  ПолучитьОписаниеОЮрФизЛице(ВыборкаПоСкладам.Контрагент, ВыборкаПоСкладам.Дата);
				
				СтруктураДанныхШапка = Новый Структура;
				СтруктураДанныхШапка.Вставить("ГарантийноеОбязательство", Заголовок);
				СтруктураДанныхШапка.Вставить("Продавец", ОписаниеОрганизации);				
				СтруктураДанныхШапка.Вставить("Покупатель", ОписаниеКонтрагента);
				
				Область.Параметры.Заполнить(СтруктураДанныхШапка);
				ТабличныйДокумент.Вывести(Область);
				
				Область = Макет.ПолучитьОбласть("ШапкаТовары");
				ТабличныйДокумент.Вывести(Область);
				
				СчетСтрок = 1;				

				ВыборкаПоСсылкамТЧ = ВыборкаПоСкладам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

				Пока ВыборкаПоСсылкамТЧ.Следующий() Цикл

					ВыборкаПоНоменклатуреТЧ = ВыборкаПоСсылкамТЧ.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

					Пока ВыборкаПоНоменклатуреТЧ.Следующий() Цикл

						ВыборкаПоСтрокамТЧ = ВыборкаПоНоменклатуреТЧ.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						
						СтрокаТовары = Макет.ПолучитьОбласть("СтрокаТовары");
						
						Пока ВыборкаПоСтрокамТЧ.Следующий() Цикл
							
							Товар = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
									ВыборкаПоСтрокамТЧ.ПредставлениеНоменклатуры,
									ВыборкаПоСтрокамТЧ.ПредставлениеХарактеристики,
									,
									ВыборкаПоСтрокамТЧ.ПредставлениеСерии,
									НоменклатураКлиентСервер.ДополнительныеПараметрыПредставлениеНоменклатурыДляПечати());
								
									
							СтрокаТовары.Параметры.Код 			= ВыборкаПоСтрокамТЧ.Код; 
							СтрокаТовары.Параметры.Товар 		= Товар;
							СтрокаТовары.Параметры.Количество 	= ВыборкаПоСтрокамТЧ.Количество;
							СтрокаТовары.Параметры.Гарантия 	= ВыборкаПоСтрокамТЧ.Гарантия;
							
							ТабличныйДокумент.Вывести(СтрокаТовары);
							
							
						КонецЦикла;
						
					КонецЦикла;
					
				КонецЦикла;
				
				Область = Макет.ПолучитьОбласть("ПодвалТоваров");
				ТабличныйДокумент.Вывести(Область);				
				Область = Макет.ПолучитьОбласть("ГарантийныеУсловия");
				ТабличныйДокумент.Вывести(Область);
				Область = Макет.ПолучитьОбласть("Подвал");
				Область.Параметры.ДатаДокумента = Формат(ВыборкаПоСкладам.Дата, "ДЛФ=DD");
				ТабличныйДокумент.Вывести(Область);
				
			КонецЦикла;
			
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
															НомерСтрокиНачало, 
															ОбъектыПечати, 
															ДанныеПечати.Ссылка);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого Ссылка Из МассивПроверки Цикл
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'В документе %1 отсутствуют товары. Печать расходной накладной не требуется.'"),
			Ссылка);
			
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			Текст,
			Ссылка);
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Если ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПечатьГарантийногоОбязательстваРасходногоОрдера(МассивОбъектов, ОбъектыПечати, ПолноеИмя) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_РасходныйОрдер_кв_ГарантийноеОбязательство";
	
	ПВХСсылка = ПолучитьСсылкуНаДополнительныйРеквизитГарантии();
	
	Макет = УправлениеПечатью.МакетПечатнойФормы(ПолноеИмя + ".ПФ_MXL_ГарантийноеОбязательство");
	
	Запрос = Новый Запрос;
	// Гарантия, мес. (ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения)
	Запрос.УстановитьПараметр("Свойство", ПВХСсылка);
	Запрос.УстановитьПараметр("Ссылка", МассивОбъектов[0]);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасходныйОрдерНаТовары.Номер КАК Номер,
	|	РасходныйОрдерНаТовары.Дата КАК Дата,
	|	РасходныйОрдерНаТовары.кв_Контрагент КАК Получатель,
	|	РасходныйОрдерНаТовары.кв_Организация КАК Продавец
	|ИЗ
	|	Документ.РасходныйОрдерНаТовары КАК РасходныйОрдерНаТовары
	|ГДЕ
	|	РасходныйОрдерНаТовары.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасходныйОрдерНаТоварыОтгружаемыеТовары.Номенклатура КАК Номенклатура,
	|   РасходныйОрдерНаТоварыОтгружаемыеТовары.Номенклатура.Код КАК Код,
	|	ПРЕДСТАВЛЕНИЕ(РасходныйОрдерНаТоварыОтгружаемыеТовары.Номенклатура) КАК ПредставлениеНоменклатуры,
	|	ПРЕДСТАВЛЕНИЕ(РасходныйОрдерНаТоварыОтгружаемыеТовары.Характеристика) КАК ПредставлениеХарактеристики,
	|	РасходныйОрдерНаТоварыОтгружаемыеТовары.Серия КАК Серия,
	|   ПРЕДСТАВЛЕНИЕ(РасходныйОрдерНаТоварыОтгружаемыеТовары.Серия) КАК ПредставлениеСерии,
	|	РасходныйОрдерНаТоварыОтгружаемыеТовары.Количество КАК Количество,
	|	ЕСТЬNULL(НоменклатураДополнительныеРеквизиты.Значение, 0) КАК Гарантия
	|ИЗ
	|	Документ.РасходныйОрдерНаТовары.ОтгружаемыеТовары КАК РасходныйОрдерНаТоварыОтгружаемыеТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура.ДополнительныеРеквизиты КАК НоменклатураДополнительныеРеквизиты
	|		ПО РасходныйОрдерНаТоварыОтгружаемыеТовары.Номенклатура = НоменклатураДополнительныеРеквизиты.Ссылка
	|			И (НоменклатураДополнительныеРеквизиты.Свойство = &Свойство)
	|ГДЕ
	|	РасходныйОрдерНаТоварыОтгружаемыеТовары.Ссылка = &Ссылка
	|
	|
	|";
	
	ПакетРезультатов = Запрос.ВыполнитьПакет();
	
	ЗапросПоШапке = ПакетРезультатов[0].Выбрать();	
	
	РеквизитыДокумента = Новый Структура("Номер, Дата");
	Покупатель	= Справочники.Контрагенты.ПустаяСсылка();
	Продавец 	= Справочники.Организации.ПустаяСсылка();
	Дата 		= Дата("00010101");
	Пока ЗапросПоШапке.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(РеквизитыДокумента, ЗапросПоШапке);		
		Покупатель	= ЗапросПоШапке.Получатель;
		Продавец	= ЗапросПоШапке.Продавец;
		Дата		= ЗапросПоШапке.Дата;
	КонецЦикла; 
	
	
	Область = Макет.ПолучитьОбласть("Шапка");
	
	Заголовок = ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(РеквизитыДокумента, 
						НСтр("ru='Гарантийное обязательство'"));
	
	ОписаниеОрганизации =  ПолучитьОписаниеОЮрФизЛице(Продавец, Дата);
	ОписаниеКонтрагента =  ПолучитьОписаниеОЮрФизЛице(Покупатель, Дата);
	
	СтруктураДанныхШапка = Новый Структура;
	СтруктураДанныхШапка.Вставить("ГарантийноеОбязательство", Заголовок);
	СтруктураДанныхШапка.Вставить("Продавец", ОписаниеОрганизации);				
	СтруктураДанныхШапка.Вставить("Покупатель", ОписаниеКонтрагента);
	
	Область.Параметры.Заполнить(СтруктураДанныхШапка);
	ТабличныйДокумент.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть("ШапкаТовары");
	ТабличныйДокумент.Вывести(Область);
	
	СтрокаТовары = Макет.ПолучитьОбласть("СтрокаТовары");
	
	ЗапросПоТоваром = ПакетРезультатов[1].Выбрать();	
	
	Пока ЗапросПоТоваром.Следующий() Цикл
		Товар = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
										ЗапросПоТоваром.ПредставлениеНоменклатуры,
										ЗапросПоТоваром.ПредставлениеХарактеристики,
										,
										ЗапросПоТоваром.ПредставлениеСерии,
										НоменклатураКлиентСервер.ДополнительныеПараметрыПредставлениеНоменклатурыДляПечати());
		
		
		СтрокаТовары.Параметры.Код 			= ЗапросПоТоваром.Код; 
		СтрокаТовары.Параметры.Товар 		= Товар;
		СтрокаТовары.Параметры.Количество 	= ЗапросПоТоваром.Количество;
		СтрокаТовары.Параметры.Гарантия 	= ЗапросПоТоваром.Гарантия;
		
		ТабличныйДокумент.Вывести(СтрокаТовары);
		
	КонецЦикла; 
	
	Область = Макет.ПолучитьОбласть("ПодвалТоваров");
	ТабличныйДокумент.Вывести(Область);				
	Область = Макет.ПолучитьОбласть("ГарантийныеУсловия");
	ТабличныйДокумент.Вывести(Область);
	Область = Макет.ПолучитьОбласть("Подвал");
	Область.Параметры.ДатаДокумента = Формат(Дата, "ДЛФ=DD");
	ТабличныйДокумент.Вывести(Область);

	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Если ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат ТабличныйДокумент;
	
КонецФункции


Функция ПолучитьОписаниеОЮрФизЛице(ЮрФизЛицо, Дата)
	
	СведенияОЮрФизЛице = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ЮрФизЛицо, Дата);
	ОписаниеОЮрФизЛице = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОЮрФизЛице, "ПолноеНаименование");
	
	Возврат ОписаниеОЮрФизЛице;
КонецФункции

Функция ПолучитьСсылкуНаДополнительныйРеквизитГарантии()
	// *- TODO: *- FIXME:
	// *- получение гарантии нужно переделать
	ЗапросНаГарантию = Новый Запрос;
	ЗапросНаГарантию.Текст = "
	|ВЫБРАТЬ
	|	ДополнительныеРеквизитыИСведения.Ссылка КАК Ссылка
	|ИЗ
	|	ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|ГДЕ
	|	ДополнительныеРеквизитыИСведения.Имя = &Имя";
	ЗапросНаГарантию.УстановитьПараметр("Имя", "Гарантия,Мес._d6bf02e8a629477da2d72a9bb9960647");
	РезультатПВХ = ЗапросНаГарантию.Выполнить();
	Если НЕ РезультатПВХ.Пустой() Тогда
		ВыборкаПВХ = РезультатПВХ.Выбрать();
		ВыборкаПВХ.Следующий();
		ПВХСсылка = ВыборкаПВХ.Ссылка;
	Иначе
		ПВХСсылка = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПустаяСсылка();
	КонецЕсли; 
	Возврат ПВХСсылка;
КонецФункции
#КонецОбласти

#КонецЕсли